apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'distribution'
sourceCompatibility = '1.7'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// NetBeans Config
if (!hasProperty('mainClass')) {
    ext.mainClass = 'arksea.azkaban.FlowLinkServer'
}

group = 'net.arksea'
version =  '1.0.0'

repositories {
    mavenLocal()
    maven { url "http://maven.oschina.net/content/repositories/thirdparty" }
    mavenCentral()
}

dependencies {
    compile(project(':azkaban-webserver'))
    compile(project(':azkaban-common'))
    compile("net.arksea:jactor:1.0.0")
    compile('mysql:mysql-connector-java:5.1.32')
    compile('org.slf4j:slf4j-api:1.6.1')
    compile('org.slf4j:slf4j-log4j12:1.6.1')
    compile('log4j:log4j:1.2.16')
    compile('com.google.guava:guava:13.0.1')
    compile('commons-codec:commons-codec:1.9')
    compile('commons-collections:commons-collections:3.2.1')
    compile('commons-configuration:commons-configuration:1.8')
    compile('commons-dbcp:commons-dbcp:1.4')
    compile('commons-dbutils:commons-dbutils:1.5')
    compile('commons-fileupload:commons-fileupload:1.2.1')
    compile('commons-io:commons-io:2.4')
    compile('commons-lang:commons-lang:2.6')
    compile('commons-logging:commons-logging:1.1.1')
    compile('commons-pool:commons-pool:1.6')
    compile('javax.mail:mail:1.4.5')
    compile('javax.servlet:servlet-api:2.5')
    compile('joda-time:joda-time:2.0')
    compile('net.sf.jopt-simple:jopt-simple:4.3')
    compile('org.apache.commons:commons-email:1.2')
    compile('org.apache.commons:commons-jexl:2.1.1')
    compile('org.apache.commons:commons-math3:3.0')
    compile('org.apache.httpcomponents:httpclient:4.3.1')
    compile('org.apache.httpcomponents:httpcore:4.3')
    compile('org.apache.velocity:velocity:1.7')
    compile('org.codehaus.jackson:jackson-core-asl:1.9.5')
    compile('org.codehaus.jackson:jackson-mapper-asl:1.9.5')
    compile('org.mortbay.jetty:jetty:6.1.26')
    compile('org.mortbay.jetty:jetty-util:6.1.26')
    testCompile "junit:junit:4.10"
}

//以下目录不打包到目标jar文件中
sourceSets {
    main {
        resources {
            exclude 'conf'
            exclude 'bin'
        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': "arksea.azkaban.FlowLinkServer"
        attributes "Class-Path": configurations.runtime.collect { 'lib/'+it.getName() }.join(' ')
    }
}

distributions{
    server{ 
        baseName = "azkaban-flowlink"
        contents {
            from "build/libs"
            into("lib"){
               from configurations.runtime
            }
            into("conf"){
                from "src/main/resources/conf"
            }
            into("bin"){
                from "src/main/resources/bin"
            }
        }
    }
}

serverDistTar.dependsOn 'assemble'
serverDistTar.compression = Compression.GZIP
serverDistTar.extension = 'tar.gz'
serverDistZip.dependsOn 'assemble'